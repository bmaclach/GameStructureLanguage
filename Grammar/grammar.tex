\documentclass{article}
\usepackage[rounded]{syntax}
\usepackage{url}

\title{Grammar for Language Describing Game Structures}
\author{Brooks MacLachlan}

\begin{document}
\maketitle

\noindent \textbf{Pretty-printed EBNF grammar:}
\begin{grammar}
<game> ::= `Players:' <teamList> `Rounds:' <roundList> `Win:' 
<winCondition> \{<tiebreaker>\}

<teamList> ::= (<team> \{`;' <team>\} | <playerList> | `randomly divide' 
<playerList> `into' <nameList>) [`all with' <attributeList>]

<team> ::= `team' <name> `:' <playerList>

<playerList> ::= <player> \{`,' <player>\}

<player> ::= <name> [`with' <attributeList>]

<attributeList> ::= <attribute> \{`,' <attribute>\}

<attribute> ::= `affiliation called' <name> | (`score' | `resource' | 
`counter') `called'
<name> [`starting at' <number>] [`with minimum of' <number>] [`with maximum of' 
<number>]

<roundList> ::= <round> \{<round>\}

<round> ::= <phaseList> [`repeated' <number> `times' [`with modifications' `:' 
<modifierList>]] `;'

<modifierList> ::= <modifier> `.' \{<modifier>\}

<modifier> ::= [`just' | `from'] (<roundReference> (`before' | `after' | 
`instead of') 
<phaseReference> `insert' <phase>)

<roundReference> ::= `round' <number>

<phaseReference> ::= `phase' <number>

<phase> ::= <action> | <progression>

<phaseList> ::= <phase> `.' \{<phase>\}

<action> ::= <competition> | <decision>

<competition> ::= [`scored'] [`team'] `competition between' <identifierList>

<decision> ::= `vote by' <identifierList> `between' 
<identifierList> [`including self'] | `nomination of' <number> `by' 
<identifierList> `between' <identifierList> [`including self'] | 
`allocation of' <name> `by' <identifierList> | `directed vote by'
<identifierList> `between' <identifierList> [`including self'] | `uses?'
<identifier> `then' `(' <phaseList> `)' [`otherwise' `(' <phaseList> `)']

<progression> ::= (<affiliationUpdate> | <counterUpdate>) (`for' | `of') 
<identifierList>

<affiliationUpdate> ::= `elimination' | (`add' | `remove') <name> | 
[`number preserving'] `swap' <nameList> [`adding' <nameList>] | `change' <name> 
`to' <name> | `merge' <nameList> [`to' <name>]

<counterUpdate> ::= (`increase' | `decrease') <name> `by' <value> | `set' 
<name> `to' <value> 

<value> ::= <number> | <name> | `results' `of' (<compReference> | 
<allocateReference> |
<voteReference>) %| <identifier> <name> for a certain player's 
%attribute

<identifierList> ::= <idValList> [`except' <idList>]

<idList> ::= <identifier> \{`,' <identifier>\}

<idValList> ::= <identifierVal> \{`,' <identifierVal>\}

<identifierVal> ::= <identifier> [`*' <value>]

<identifier> ::= `everyone' | <name> | `chance'  <number> `(' <identifierList> 
`)' | 
`nominated' 
| 
`tied' | 
`eliminated' | (`winner' | `loser') `of' <compReference> | (`majority' | 
`minority') `of' <voteReference>  [<tiebreakerReference>] | (`highest' | `lowest' | 
`most' | `least') <name> `(' <identifierList> `)' [<tiebreakerReference>]

<compReference> ::= `competition' [<number>] 

<allocateReference> ::= `allocation' [<number>]

<voteReference> ::= `vote' [<number>]

<tiebreaker> ::= `Tiebreaker:' <name> [<action>] <identifier>

<tiebreakerReference> ::= `tiebroken by' <name>

<name> ::= (`a' | ... | `z' | `A' | ... | `Z') \{`a' | ... | `z' | `A' | ... | 
`Z' | `0' | ... | `9'\}

<nameList> ::= <name> \{`,' <name>\}

<number> ::= [`-'] (`0' | ... | `9')+

<winCondition> ::= `reach' <goalList> [`for team']  | [`team'] 
`competition' | <identifierList> [`for team'] | 
<number> `member jury vote' | `survive'

<goal> ::= <number> <name>

<goalList> ::= <goal> \{`,' <goal>\}

\end{grammar}

TODO:
\begin{itemize}
  \item Either add a tiebreaker reference to the "winner/loser of competition" identifier, specifically for scored competitions (since they could tie), or add the possibility of multiple actions in a tiebreaker, so that the results of a scored competition could increment a counter, which can then be used in an identifier with a tiebreaker. Probably both of these should be done.
  \item comments
\end{itemize}
\end{document}